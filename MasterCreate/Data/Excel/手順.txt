下記のマクロを個人用マクロブック personal.xlsbに登録しておく。
尚、personal.xlsbは表示メニューから非表示扱いにして保存しないと立ち上げ時に開かれてしまうため注意。

============================================================
Sub OutputCsv()
    '
    ' 変数の宣言
    '
    Dim maxRow, maxColumn As Long
    Dim fileSystemObject As Object
    Dim csvFileName As String
    Dim fileNo As Integer
    Dim x, y As Long
    Dim messageReturn As Integer

    messageReturn = MsgBox("CSVファイルを出力します", vbYesNo)
    If messageReturn <> vbYes Then
        MsgBox "キャンセルしました。"
        Exit Sub
    End If
 
    '
    ' 最終行、最終列の取得。
    ' 参考: http://www.niji.or.jp/home/toru/notes/8.html
    '
    With ActiveSheet.UsedRange
        maxRow = .Find("*", , xlFormulas, , xlByRows, xlPrevious).Row
        maxColumn = .Find("*", , xlFormulas, , xlByColumns, xlPrevious).Column
    End With

    '
    ' 書き出し先とするcsvファイル名の決定
    '
    Set fileSystemObject = CreateObject("Scripting.FileSystemObject")
    csvFileName = fileSystemObject.GetParentFolderName(ActiveWorkbook.Path) & "\Csv\" & fileSystemObject.GetBaseName(ActiveWorkbook.Name) & ".csv"
    
    '
    ' csvファイルに書き込み。
    '
    fileNo = FreeFile
    Open csvFileName For Output As #fileNo

    ' 仕様として1行目は無視。
    For y = 2 To maxRow
        For x = 1 To maxColumn
            ' 列名が入る行が空白の場合はスキップ
            If Cells(2, x).Value = "" Then
                ' goto!
                GoTo Continue
            End If


            ' NULLと記述のある箇所はnull扱いであり文字列のNULLとしては扱わない。特殊な仕様。
            If Cells(y, x).Value <> "NULL" Then
                tmpStr = """" & sanitize(Cells(y, x)) & """"
            Else
                tmpStr = "NULL"
            End If


            If x <> maxColumn Then
                ' 尻の; は改行無しを表す
                Print #fileNo, tmpStr & ",";
            Else
                ' 改行を挿入する
                Print #fileNo, tmpStr & vbLf;
            End If
Continue:
        Next


    Next

    ' ファイルを閉じる
    Close #fileNo
    
    MsgBox ("CSVファイルを出力しました。" & csvFileName)

End Sub

Function sanitize(Value As String) As String
    Value = Replace(Value, vbCrLf, "\n")
    Value = Replace(Value, vbCr, "\n")
    Value = Replace(Value, vbLf, "\n")
    sanitize = Replace(Value, """", ChrW(8221))
    
End Function
